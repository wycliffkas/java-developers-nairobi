apply plugin: 'com.android.application'
apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.0'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ])
}

android {
    compileSdkVersion rootProject.compileSdk
    defaultConfig {
        applicationId "com.example.javadevelopersnairobi.javadevelopersnairobi"
        minSdkVersion rootProject.minSdk
        targetSdkVersion rootProject.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests.returnDefaultValues = true

        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$rootProject.androidSupportLibraryVersion"
    implementation "com.android.support:exifinterface:$rootProject.androidSupportLibraryVersion"
    implementation "com.android.support:design:$rootProject.androidSupportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.constraintLayoutVersion"
    implementation "com.google.code.gson:gson:$rootProject.googleGsonVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.androidSupportLibraryVersion"
    implementation "com.android.support:cardview-v7:$rootProject.androidSupportLibraryVersion"
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    implementation "com.android.support:support-annotations:$rootProject.androidSupportLibraryVersion"
    implementation "android.arch.persistence.room:runtime:$rootProject.roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$rootProject.roomVersion"
    implementation "android.arch.lifecycle:extensions:$rootProject.archLifecycleVersion"
    androidTestImplementation "android.arch.persistence.room:testing:$rootProject.roomVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$rootProject.glideVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$rootProject.archLifecycleVersion"
    testImplementation "junit:junit:$rootProject.jUnitVersion"
    androidTestImplementation "com.android.support.test:runner:$rootProject.testRunner"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.espresso"
    androidTestImplementation "com.android.support:support-annotations:$rootProject.androidSupportLibraryVersion"
    androidTestImplementation "com.android.support.test:rules:$rootProject.testRule"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$rootProject.espresso"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$rootProject.espressoIntents"
    testImplementation "org.robolectric:robolectric:$rootProject.robolectric"
    androidTestUtil "com.android.support.test:orchestrator:$rootProject.orchestrator"
}
